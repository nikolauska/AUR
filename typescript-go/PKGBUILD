# Maintainer: Niko Lehtovirta <niko@lehtovirta.dev>

pkgname=typescript-native
pkgver=20260221
pkgrel=1
pkgdesc="Native typescript"
url="https://www.npmjs.com/package/@typescript/native-preview"

arch=("x86_64")
options=(!strip !debug emptydirs staticlibs zipman)
license=("Apache-2.0")

depends=("nodejs")
makedepends=("npm" "jq")

source=("http://registry.npmjs.org/@typescript/native-preview/-/native-preview-7.0.0-dev.${pkgver}.${pkgrel}.tgz")
noextract=("native-preview-7.0.0-dev.${pkgver}.${pkgrel}.tgz")

sha1sums=('c4a9f3f35bbc0c41c32491c9ab8be680b33cd569')

package() {
  printf '  -> Installing with npm\n'
  npm install -s -g \
    --cache "${srcdir}/npm-cache" \
    --prefix "${pkgdir}/usr" \
    "${srcdir}/native-preview-7.0.0-dev.${pkgver}.${pkgrel}.tgz"

  printf '  -> Fixing ownership for packaged files\n'
  find "${pkgdir}/usr" -type d -exec chmod 755 {} +
  chown -R root:root "${pkgdir}"

  printf '  -> Removing PKGDIR references from package metadata\n'
  find "${pkgdir}" -name package.json -print0 | xargs -r -0 sed -i '/_where/d'

  printf "  -> Cleaning package.json keys\n"
  local pkgjson="${pkgdir}/usr/lib/node_modules/@typescript/native-preview/package.json"
  local tmppackage
  tmppackage="$(mktemp)"
  jq '.|=with_entries(select(.key|test("_.+")|not))' "${pkgjson}" > "${tmppackage}"
  mv "${tmppackage}" "${pkgjson}"
  chmod 644 "${pkgjson}"

  printf "  -> Removing unsupported man entries from package.json files\n"
  find "${pkgdir}" -type f -name package.json | while IFS= read -r pkgjson; do
    local tmppackage
    tmppackage="$(mktemp)"
    jq 'del(.man)' "${pkgjson}" > "${tmppackage}"
    mv "${tmppackage}" "${pkgjson}"
    chmod 644 "${pkgjson}"
  done
}
