# Maintainer: Niko Lehtovirta <niko@lehtovirta.dev>

pkgname=tidewave-app
pkgver=0.3.5
pkgrel=1
pkgdesc="Tidewave Desktop App"
arch=('x86_64')
url="https://github.com/tidewave-ai/tidewave_app"
license=('Apache-2.0')
depends=()
provides=('tidewave-app')
options=('!strip' '!debug')

source=(
  "tidewave-app-amd64-${pkgver}-${pkgrel}.AppImage::https://github.com/tidewave-ai/tidewave_app/releases/download/v${pkgver}/tidewave-app-amd64.AppImage"
)
sha256sums=('00e2ffb0fd3c17f04ba715cf9cb18ec4561889fc236b1a12dbda3401a884bef5')

package() {
  local srcdir="${srcdir:?srcdir is not set}"
  local pkgdir="${pkgdir:?pkgdir is not set}"
  local appimage="tidewave-app-amd64-${pkgver}-${pkgrel}.AppImage"

  install -Dm755 \
    "$srcdir/$appimage" \
    "$pkgdir/usr/lib/$pkgname/$pkgname.AppImage"

  install -dm755 "$pkgdir/usr/bin"
  ln -s "../lib/$pkgname/$pkgname.AppImage" "$pkgdir/usr/bin/$pkgname"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/$pkgname.desktop" <<EOF
[Desktop Entry]
Type=Application
Name=Tidewave
Comment=Tidewave Desktop App
Exec=$pkgname
Icon=applications-development
Categories=Development;
Terminal=false
EOF
}
