pkgname=tidewave-app
pkgver=0.3.0
pkgrel=1
pkgdesc="Tidewave Desktop App"
arch=('x86_64')
url="https://github.com/tidewave-ai/tidewave_app"
license=('Apache-2.0')
depends=('glibc')
provides=('tidewave-app')
options=('!strip')

source=(
  "tidewave-app-amd64-${pkgver}-${pkgrel}.AppImage::https://github.com/tidewave-ai/tidewave_app/releases/download/v${pkgver}/tidewave-app-amd64.AppImage"
)
sha256sums=('8436d987d2124ff59e15d80d6ed948a4e5fff288131849114e2893104ff3c5a0')

prepare() {
  cd "$srcdir"

  chmod +x "tidewave-app-amd64-${pkgver}-${pkgrel}.AppImage"
  ./tidewave-app-amd64-${pkgver}-${pkgrel}.AppImage --appimage-extract
}

package() {
  cd "$srcdir"

  install -d "$pkgdir/opt/$pkgname"
  cp -a "squashfs-root/." "$pkgdir/opt/$pkgname/"
  chmod -R u+rwX,go+rX "$pkgdir/opt/$pkgname"

  install -Dm755 /dev/stdin "$pkgdir/usr/bin/tidewave-app" <<EOF
#!/bin/sh
exec "/opt/$pkgname/AppRun" "\$@"
EOF

  if [[ -f squashfs-root/usr/share/applications/Tidewave.desktop ]]; then
    install -Dm644 \
      squashfs-root/usr/share/applications/Tidewave.desktop \
      "$pkgdir/usr/share/applications/$pkgname.desktop"
    sed -i \
      -e 's|^Categories=.*|Categories=Development;|g' \
      -e 's|^Exec=.*|Exec=tidewave-app|g' \
      -e "s|^Icon=.*|Icon=$pkgname|g" \
      "$pkgdir/usr/share/applications/$pkgname.desktop"
  fi

  if [[ -d squashfs-root/usr/share/icons/hicolor ]]; then
    install -d "$pkgdir/usr/share/icons"
    cp -a squashfs-root/usr/share/icons/hicolor "$pkgdir/usr/share/icons/"
  fi
}
